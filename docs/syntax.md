# 文法

## Lua 5.5のEBNF

- https://www.lua.org/manual/5.5/manual.html#9

```
chunk ::= block

block ::= {stat} [retstat]

stat ::= ';' |
    varlist '=' explist |
    functioncall |
    label |
    'break' |
    'goto' Name |
    'do' block 'end' |
    'while' exp 'do' block 'end' |
    'repeat' block 'until' exp |
    'if' exp 'then' block {'elseif' exp 'then' block} ['else' block] 'end' |
    'for' Name '=' exp ',' exp [',' exp] 'do' block 'end' |
    'for' namelist 'in' explist 'do' block 'end' |
    'function' funcname funcbody |
    'local' 'function' Name funcbody |
    'global' 'function' Name funcbody |
    'local' attnamelist ['=' explist] |
    'global' attnamelist |
    'global' [attrib] '*'

attnamelist ::= [attrib] Name [attrib] {',' Name [attrib]}

attrib ::= '<' Name '>'

retstat ::= 'return' [explist] [';']

label ::= '::' Name '::'

funcname ::= Name {'.' Name} [':' Name]

varlist ::= var {',' var}

var ::= Name | prefixexp '[' exp ']' | prefixexp '.' Name

namelist ::= Name {',' Name}

explist ::= exp {',' exp}

exp ::= 'nil' | 'false' | 'true' | Numeral | LiteralString | '...' | functiondef |
    prefixexp | tableconstructor | exp binop exp | unop exp

prefixexp ::= var | functioncall | '(' exp ')'

functioncall ::= prefixexp args | prefixexp ':' Name args

args ::= '(' [explist] ')' | tableconstructor | LiteralString

functiondef ::= 'function' funcbody

funcbody ::= '(' [parlist] ')' block 'end'

parlist ::= namelist [',' varargparam] | varargparam

varargparam ::= '...' [Name]

tableconstructor ::= '{' [fieldlist] '}'

fieldlist ::= field {fieldsep field} [fieldsep]

field ::= '[' exp ']' '=' exp | Name '=' exp | exp

fieldsep ::= ',' | ';'

binop ::= '+' | '-' | '*' | '/' | '//' | '^' | '%' |
    '&' | '~' | '|' | '>>' | '<<' | '..' |
    '<' | '<=' | '>' | '>=' | '==' | '~=' |
    'and' | 'or'

unop ::= '-' | 'not' | '#' | '~'
```

## Lua 5.5の演算子と優先順位

- https://www.lua.org/manual/5.5/manual.html#3.4.8

```
or
and
<     >     <=    >=    ~=    ==
|
~
&
<<    >>
..
+     -
*     /     //    %
unary operators (not   #     -     ~)
^
```

## prefixexp

- `prefixexp`は左再帰が循環して扱いにくいので演算子で整理する。
- `prefixexp`は`Name`か`'(' exp ')'`で開始する。
- どの演算子も左結合で同じ優先順位を持つ。

| 表現     | 文法                                     | 名前      | 説明                   |
|----------|------------------------------------------|-----------|------------------------|
| `a[b]`   | `prefixexp '[' exp ']'`                  | subscript | subscript              |
| `a.b`    | `prefixexp '.' Name`                     | field     | field access           |
| `a(b)`   | `prefixexp '(' [explist] ')'`            | call      | function call          |
| `a{b}`   | `prefixexp '{' [fieldlist] '}'`          | call      | function call (table)  |
| `a"b"`   | `prefixexp LiteralString`                | call      | function call (string) |
| `a:b(c)` | `prefixexp ':' Name '(' [explist] ')'`   | self      | method call            |
| `a:b{c}` | `prefixexp ':' Name '{' [fieldlist] '}'` | self      | method call (table)    |
| `a:b"c"` | `prefixexp ':' Name LiteralString`       | self      | method call (string)   |

